fs     = require 'fs'
{exec} = require 'child_process'

appFiles  = [
    # omit src/ and .coffee to make the below lines a little shorter
    'init'
    'window'
    'document'
    'node'
    'element'
    'string'
    'run'
]

task 'build', 'Build single application file from source files', ->
  appContents = new Array remaining = appFiles.length
  for file, index in appFiles then do (file, index) ->
    fs.readFile "src/#{file}.coffee", 'utf8', (err, fileContents) ->
      throw err if err
      appContents[index] = """\n\nconsole.log "Evaluating: #{file}.coffee"\n\n""" + fileContents
      process() if --remaining is 0
  process = ->
    fs.writeFile 'main.coffee', appContents.join("\n\n"), 'utf8', (err) ->
      throw err if err
      exec 'coffee --output ../js/lib/ --compile main.coffee', (err, stdout, stderr) ->
        throw err if err
        console.log stdout + stderr
        fs.unlink 'main.coffee', (err) ->
          throw err if err
          console.log 'Done.'


task 'minify', 'Minify the resulting application file after build', ->
  exec 'java -jar "/home/stan/public/compiler.jar" --js lib/app.js --js_output_file lib/app.production.js', (err, stdout, stderr) ->
    throw err if err
    console.log stdout + stderr


task 'docs', 'Create the documentation', ->
    ""
    
